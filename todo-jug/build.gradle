apply from: "$rootDir/gradle/versioning.gradle"
apply from: "./libraries.gradle"


ext {
    modelProjects = ["model"]
}


subprojects { subProject ->
    apply plugin: 'java'
    apply from: "$rootDir/gradle/integrationTests.gradle"

    apply plugin: fr.fteychene.gradlejug.buildsrc.version.BuildVersionPlugin
    // Publication
    apply plugin: 'maven-publish'
    // Quality
    apply plugin: 'checkstyle'
    apply plugin: "jacoco"
    // Reports
    apply plugin: 'build-dashboard'
    apply plugin: 'project-report'

    defaultTasks 'build'

    sourceCompatibility = 1.8

    buildVersion {
        defaultVersion = "1.0.0"
    }

    group = "fr.fteychene.gradlejug"
    version = buildVersion.version

    repositories {
        mavenCentral()
    }

    ext {
        isModelProject = subProject.name in rootProject.modelProjects
    }

    dependencies {
        compile libraries.guava

        if (!isModelProject) {
            compile libraries.spring
            compile libraries.javaassist
            compile project(':model')
        }
        testCompile libraries.junit
    }

    test {
        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }
    }

    checkstyle {
        sourceSets = [subProject.sourceSets.main]
        configFile = rootProject.file('shared/config/checkstyle/checkstyle.xml')
        showViolations = false
        ignoreFailures = true
    }

    jacocoTestReport() {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination "${buildDir}/jacoco"
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}