apply from: "$rootDir/gradle/versioning.gradle"
apply from: "./libraries.gradle"


subprojects { subProject ->
    apply plugin: 'java'
    apply plugin: fr.fteychene.gradlejug.buildsrc.version.BuildVersionPlugin
    apply from: "$rootDir/gradle/integrationTests.gradle"

    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: 'build-dashboard'
    apply plugin: 'project-report'

    defaultTasks 'build'

    sourceCompatibility = 1.8

    buildVersion {
        defaultVersion = "1.0.0"
    }

    version = buildVersion.version

    repositories {
        mavenCentral()
    }

    dependencies {
        compile libraries.guava
        testCompile libraries.junit
    }

    test {
        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }
    }

    checkstyle {
        sourceSets = [subProject.sourceSets.main]
        configFile = rootProject.file('shared/config/checkstyle/checkstyle.xml')
        showViolations = false
        ignoreFailures = true
    }
}

//gradle.taskGraph.whenReady {taskGraph ->
//    if (taskGraph.hasTask(release)) {
//        projectVersions.release = true
//    }
//}


task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}