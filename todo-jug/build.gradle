// Import des librairies comme
apply from: "./libraries.gradle"

// Extra properties
ext {
    modelProjects = ["model"]
}

// Gestion commune des projets
subprojects { subProject ->
    apply plugin: 'java'
    // Custom (test intégration + gestion version)
    apply from: "$rootDir/gradle/integrationTests.gradle"
    apply plugin: fr.fteychene.gradlejug.buildsrc.version.BuildVersionPlugin
    // Publication
    apply plugin: 'maven-publish'
    // Quality
    apply plugin: 'checkstyle'
    apply plugin: "jacoco"
    // Reports
    apply plugin: 'build-dashboard'
    apply plugin: 'project-report'

    defaultTasks 'build'

    sourceCompatibility = 1.8
    group = "fr.fteychene.gradlejug"
    version = buildVersion.version

    ext {
        isModelProject = subProject.name in rootProject.modelProjects
    }

    // Configuration du plugin custom BuildVersionPlugin
    buildVersion {
        defaultVersion = "1.0.0"
    }

    // Gestion repositories
    repositories {
        mavenCentral()
    }

    // Dependances communes à tous les projets
    dependencies {
        compile libraries.guava

        if (!isModelProject) {
            compile libraries.spring
            compile libraries.javaassist
            compile project(':model')
        }
        testCompile libraries.junit
    }

    // Ajout d'un log pour le run des tests
    test {
        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }
    }

    // Configuration checkstyle
    checkstyle {
        sourceSets = [subProject.sourceSets.main]
        configFile = rootProject.file('shared/config/checkstyle/checkstyle.xml')
        showViolations = false
        ignoreFailures = true
    }

    // Configuration jacoco
    jacocoTestReport() {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination "${buildDir}/jacoco"
        }
    }
    check.dependsOn jacocoTestReport
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}