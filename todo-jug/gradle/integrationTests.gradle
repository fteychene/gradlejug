configurations {
    integrationCompile
    integrationRuntime
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration/java')
        resources.srcDir file('src/integration/resources')
        compileClasspath = sourceSets.main.output + configurations.integrationCompile
        runtimeClasspath = output + compileClasspath + configurations.integrationRuntime
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    //reports.junitXml.destination = file("$reports.junitXml.destination/integration")
    reports.html.destination = file("$reports.html.destination/../integration")

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running integration test: " + descriptor)
    }
}

check.dependsOn integrationTest
compileIntegrationTestJava.mustRunAfter test